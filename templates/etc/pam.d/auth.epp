<%-|
  String[1]                      $name,
  Pam::PasswordBackends          $password_check_backend,
  Optional[Stdlib::Absolutepath] $locale_file,
  Optional[Array[String]]        $auth_content_pre,
  Boolean                        $cracklib_enforce_for_root,
  Boolean                        $cracklib_reject_username,
  Optional[Integer[0]]           $cracklib_difok,
  Optional[Integer[0]]           $cracklib_maxrepeat,
  Optional[Integer[0]]           $cracklib_maxsequence,
  Optional[Integer[0]]           $cracklib_maxclassrepeat,
  Optional[Boolean]              $cracklib_gecoscheck,
  Optional[Integer]              $cracklib_dcredit,
  Optional[Integer]              $cracklib_ucredit,
  Optional[Integer]              $cracklib_lcredit,
  Optional[Integer]              $cracklib_ocredit,
  Optional[Integer[0]]           $cracklib_minclass,
  Optional[Integer[0]]           $cracklib_minlen,
  Variant[Boolean, Integer[0]]   $cracklib_retry,
  Boolean                        $nullok,
  Integer[0]                     $deny,
  Boolean                        $faillock,
  Boolean                        $manage_faillock_conf,
  Boolean                        $faillock_audit,
  Boolean                        $faillock_no_log_info,
  Boolean                        $faillock_local_users_only,
  Boolean                        $faillock_nodelay,
  Optional[String]               $faillock_admin_group,
  Optional[Stdlib::Absolutepath] $faillock_log_dir,
  Boolean                        $display_account_lock,
  Integer[0]                     $fail_interval,
  Boolean                        $manage_pwhistory_conf,
  Boolean                        $remember_debug,
  Integer[0]                     $remember,
  Integer[0]                     $remember_retry,
  Boolean                        $remember_for_root,
  StdLib::Absolutepath           $remember_file,
  Boolean                        $even_deny_root,
  Integer[0]                     $root_unlock_time,
  Pam::HashAlgorithm             $hash_algorithm,
  Integer[0]                     $rounds,
  Integer[0]                     $uid,
  Pam::AccountUnlockTime         $unlock_time,
  Boolean                        $preserve_ac,
  Boolean                        $use_netgroups,
  Boolean                        $use_openshift,
  Boolean                        $sssd,
  Array[String[0]]               $tty_audit_users,
  String[0]                      $separator,
  Boolean                        $enable_separator,
  Boolean                        $oath,
  Integer[0]                     $oath_window,
  Optional[Integer[0]]           $inactive,
  Optional[String[0]]            $pam_cert_auth
|-%>
#%PAM-1.0
# This file managed by Puppet
# User changes will be lost!
<%
if $even_deny_root {
  $_even_deny_root = " even_deny_root root_unlock_time=${root_unlock_time}"
}
else {
  $_even_deny_root = ''
}
-%>
<% unless empty($auth_content_pre) { -%>

# User defined prepended auth content
<%   $auth_content_pre.each |String $auth_pre_line| { -%>
<%= $auth_pre_line %>
<%   } -%>
<% } -%>

# SIMP defined auth content
auth     optional      pam_faildelay.so
auth     required      pam_env.so
<% if $faillock {
     if $manage_faillock_conf {
-%>
auth     required      pam_faillock.so preauth
<%   } else {
  if $faillock_log_dir {
    $_faillock_log_dir = " dir=${faillock_log_dir}"
  }
  else {
    $_faillock_log_dir = ''
  }

  if $faillock_audit {
    $_faillock_audit = ' audit'
  }
  else {
    $_faillock_audit = ''
  }

  if $faillock_no_log_info {
    $_faillock_no_log_info = ' no_log_info'
  }
  else {
    $_faillock_no_log_info = ''
  }

  if $faillock_local_users_only {
    $_faillock_local_users_only = ' local_users_only'
  }
  else {
    $_faillock_local_users_only = ''
  }

  if $faillock_nodelay {
    $_faillock_nodelay = ' nodelay'
  }
  else {
    $_faillock_nodelay = ''
  }

  if $faillock_admin_group {
    $_faillock_admin_group = " admin_group=${faillock_admin_group}"
  }
  else {
    $_faillock_admin_group = ''
  }

  if $display_account_lock {
    $_display_account_lock = ''
  }
  else {
    $_display_account_lock = ' silent'
  }
-%>
auth     required      pam_faillock.so preauth<%= $_display_account_lock %> deny=<%= $deny %><%= $_faillock_audit %> unlock_time=<%= $unlock_time %> fail_interval=<%= $fail_interval %><%= $_faillock_log_dir %><%= $_faillock_no_log_info %><%= $_faillock_local_users_only %><%= $_faillock_nodelay %><%= $_faillock_admin_group %><%= $_even_deny_root %>
<%   } -%>
<% } -%>
<%
if $pam_cert_auth {
  $_pam_cert_auth = " ${pam_cert_auth}"
}
else {
  $_pam_cert_auth = ''
}
-%>
<% if $name == 'fingerprint' { -%>
auth     sufficient    pam_fprintd.so
<% } -%>
<% if $name == 'smartcard' { -%>
auth     [success=done ignore=ignore default=die] pam_pkcs11.so wait_for_card card_only
<% } -%>
<% if $oath and ($name == 'system') { -%>
auth     [success=3 default=ignore] pam_listfile.so item=group sense=allow file=/etc/liboath/exclude_groups.oath quiet
auth     [success=2 default=ignore] pam_listfile.so item=user sense=allow file=/etc/liboath/exclude_users.oath quiet
auth     [success=1 default=bad]    pam_oath.so usersfile=/etc/liboath/users.oath window=<%= $oath_window %>
auth     requisite     pam_deny.so
<% } -%>
<% if $name in ['system', 'password'] { -%>
<%   if $faillock { -%>
<%     if $sssd { -%>
auth     [success=2 default=ignore] pam_sss.so forward_pass<%= $_pam_cert_auth %>
<%     } -%>
<%     if $inactive { -%>
auth required pam_lastlog.so inactive=<%= $inactive %>
<%     } -%>
auth     [success=1 default=ignore] pam_unix.so try_first_pass
<%       if $manage_faillock_conf { -%>
auth     [default=die] pam_faillock.so authfail
auth     sufficient    pam_faillock.so authsucc
<%       } else { -%>
auth     [default=die] pam_faillock.so authfail deny=<%= $deny %> audit unlock_time=<%= $unlock_time %><%= $_even_deny_root %>
auth     sufficient    pam_faillock.so authsucc deny=<%= $deny %> audit unlock_time=<%= $unlock_time %><%= $_even_deny_root %>
<%       } -%>
<%   } else { -%>
<%     if $sssd { -%>
auth     sufficient    pam_sss.so forward_pass<%= $_pam_cert_auth %>
<%     } -%>
auth     [default=1 ignore=ignore success=ok] pam_succeed_if.so uid >= <%= $uid %> quiet
auth     [default=1 success=ok] pam_localuser.so
<%     if $inactive { -%>
auth required pam_lastlog.so inactive=<%= $inactive %>
<%     } -%>
auth     sufficient    pam_unix.so try_first_pass
<%   } -%>
<% } -%>
auth     requisite     pam_succeed_if.so uid >= <%= $uid %> quiet
auth     required      pam_deny.so

<% if $use_netgroups { -%>
<%   if $enable_separator { -%>
account     required      pam_access.so listsep=<%= $separator %> accessfile=/etc/security/access.conf
<%   } else { -%>
account     required      pam_access.so accessfile=/etc/security/access.conf
<%   } -%>
<% } -%>
<% if $faillock { -%>
account     required      pam_faillock.so
<% } -%>
account     required      pam_unix.so broken_shadow
<% if $sssd { -%>
<%   if $use_openshift { -%>
account     [success=4 default=ignore] pam_succeed_if.so quiet shell = /usr/bin/oo-trap-user
<%   } -%>
account     [success=3 default=ignore] pam_succeed_if.so service = crond quiet
account     sufficient    pam_succeed_if.so uid < <%= $uid %> quiet
<%   if $enable_separator { -%>
account     requisite     pam_access.so listsep=<%= $separator %> nodefgroup
<%   } else { -%>
account     requisite     pam_access.so nodefgroup
<%   } -%>
account     [success=1 default=ignore] pam_localuser.so
account     [default=bad success=ok system_err=ignore user_unknown=ignore] pam_sss.so
<% } else { -%>
<%   if $use_openshift { -%>
account     [success=3 default=ignore] pam_succeed_if.so quiet shell = /usr/bin/oo-trap-user
<%   } -%>
account     [success=2 default=ignore] pam_succeed_if.so service = crond quiet
account     sufficient    pam_succeed_if.so uid < <%= $uid %> quiet
<%   if $enable_separator { -%>
account     requisite     pam_access.so listsep=<%= $separator %> nodefgroup
<%   } else { -%>
account     requisite     pam_access.so nodefgroup
<%   } -%>
<% } -%>
account     required      pam_permit.so

<% if $name in ['system', 'password'] { -%>
<%   if $cracklib_retry {
       $_cracklib_retry = " retry=${cracklib_retry}"
     } else {
       $_cracklib_retry = ''
     }

     if $cracklib_enforce_for_root {
       $_cracklib_enforce_for_root = ' enforce_for_root'
     } else {
       $_cracklib_enforce_for_root = ''
     }

     if $cracklib_reject_username {
       $_cracklib_reject_username = ' reject_username'
     } else {
       $_cracklib_reject_username = ''
     }

     $_pam_password_check = "password     requisite     pam_${password_check_backend}.so${_cracklib_retry}${_cracklib_enforce_for_root}${_cracklib_reject_username}"
-%>
<%   if ($password_check_backend == 'cracklib') { -%>
<%=     $_pam_password_check -%>
<%       if $cracklib_minlen { %> minlen=<%= $cracklib_minlen %><% } -%>
<%       if $cracklib_minclass { %> minclass=<%= $cracklib_minclass %><% } -%>
<%       if $cracklib_maxrepeat { %> maxrepeat=<%= $cracklib_maxrepeat %><% } -%>
<%       if $cracklib_difok { %> difok=<%= $cracklib_difok %><% } -%>
<%       if $cracklib_maxsequence { %> maxsequence=<%= $cracklib_maxsequence %><% } -%>
<%       if $cracklib_maxclassrepeat { %> maxclassrepeat=<%= $cracklib_maxclassrepeat %><% } -%>
<%       if $cracklib_dcredit { %> dcredit=<%= $cracklib_dcredit %><% } -%>
<%       if $cracklib_ucredit { %> ucredit=<%= $cracklib_ucredit %><% } -%>
<%       if $cracklib_lcredit { %> lcredit=<%= $cracklib_lcredit %><% } -%>
<%       if $cracklib_ocredit { %> ocredit=<%= $cracklib_ocredit %><% } -%>
<%       if $cracklib_gecoscheck { %> gecoscheck<% } -%>
<%       %>
<%     } else {-%>
<%= $_pam_password_check %>
<%     }-%>
<%
    if $manage_pwhistory_conf {
      $_pam_pwhistory = 'password     required      pam_pwhistory.so use_authtok'
    } else {
      if $remember_debug {
        $_remember_debug = ' debug'
      } else {
        $_remember_debug = ''
      }

      if $remember_for_root {
        $_remember_for_root = ' enforce_for_root'
      } else {
        $_remember_for_root = ''
      }

      $_pam_pwhistory = "password     required      pam_pwhistory.so use_authtok remember=${remember} retry=${remember_retry} file=${remember_file}${_remember_debug}${_remember_for_root}"
-%>
<%  } -%>
<%
     if $nullok {
      $_nullok = 'nullok '
     } else {
      $_nullok = ''
     }
-%>
<%   $_pam_unix = "password     sufficient    pam_unix.so ${_nullok}${hash_algorithm} rounds=${rounds} shadow try_first_pass use_authtok" -%>
<%   if $sssd { -%>
<%= $_pam_pwhistory %>
<%= $_pam_unix %>
password     sufficient    pam_sss.so use_authtok
<%   } else { -%>
<%= $_pam_pwhistory %>
<%= $_pam_unix %>
<%   } -%>
<% } -%>
<% if $name == 'smartcard' { -%>
password     required      pam_pkcs11.so
<% } else { -%>
password     required      pam_deny.so
<% } -%>

session      optional      pam_keyinit.so revoke
session      required      pam_limits.so
-session     optional      pam_systemd.so
session      sufficient    pam_succeed_if.so service = gdm-launch-environment quiet
session      sufficient    pam_succeed_if.so service in crond quiet use_uid
<% if ($name in ['system','password','fingerprint']) and !empty($tty_audit_users) { -%>
# Check if session has a tty before running pam_tty_audit
session      [default=ignore success=1] pam_succeed_if.so tty !~ ?* quiet
<%-   if dig($facts, 'simplib__auditd', 'enforcing') { -%>
session      required      pam_tty_audit.so disable=* enable=<%= $tty_audit_users.join(',') %>
<%-   } else { -%>
# auditd disabled: pam_tty_audit set to optional so that all logins do not fail
session      optional      pam_tty_audit.so disable=* enable=<%= $tty_audit_users.join(',') %>
<%-  } -%>
<% } -%>
<% if $locale_file { -%>
session      optional      pam_env.so readenv=1 envfile=<%= $locale_file %>
<% } -%>
session      sufficient    pam_succeed_if.so user = root quiet
<% if $use_openshift { -%>
session      [default=1 success=ignore] pam_succeed_if.so quiet shell = /usr/bin/oo-trap-user
session      required      pam_namespace.so no_unmount_on_close
session      [default=ignore success=1] pam_succeed_if.so quiet shell = /usr/bin/oo-trap-user
<% } -%>
<% if $enable_separator { -%>
session      requisite     pam_access.so listsep=<%= $separator %> nodefgroup
<% } else { -%>
session      requisite     pam_access.so nodefgroup
<% } -%>
<% if $sssd { -%>
session      optional      pam_sss.so
<% } else { -%>
session      required      pam_unix.so
<% } -%>
session      optional      pam_oddjob_mkhomedir.so silent
session      required      pam_lastlog.so showfailed
